// New scripts start here.
// VAR_PORT_TOSA_STATE
// 0 = Reserve gate closed, no permit yet.
// 1 = Permit obtained, gate open.

mapscripts LittlerootTown_MapScripts {
	// This mapscript hides the gatekeeper if the reserve gate is closed.
	MAP_SCRIPT_ON_TRANSITION {
		if(var(VAR_PORT_TOSA_STATE) == 0) {
			setflag(FLAG_HIDE_RESERVE_GATEKEEPER)
    	}
	}
}

script LittlerootTown_EventScript_ProfPalmSign {
    lockall
    msgbox("Professor Palm's Laboratory\n{COLOR LIGHT_GRAY}Driving Humans & Pokémon Forward", MSGBOX_SIGN)
    releaseall
}

script LittlerootTown_EventScript_TrainerPermit {
	lockall
	setflag(FLAG_ITEM_LITTLEROOT_TRAINER_PERMIT)
	removeobject(3)
	giveitem(ITEM_TRAINER_PERMIT, 1)
	releaseall
	end
}

script LittlerootTown_EventScript_TestTrainer {
	trainerbattle_single(TRAINER_TEST_TRAINER, Trainer_Intro, Trainer_Outro)
	msgbox(Trainer_PostBattle, MSGBOX_AUTOCLOSE)
	waitmessage
	end
}

text Trainer_Intro {
	"Ugh..I'm totally lost! Might as well stop\nfor a battle!"
}

text Trainer_Outro {
	"I've totally lost my way..."
}

text Trainer_PostBattle {
	"Make sure you pay close attention to\nyour map.\p"
	"The nature reserve is a big place!"
}

script LittlerootTown_ProfPalmBag {
	lockall
	faceplayer
	if (flag(FLAG_FOUNDSTARTER)) {
		msgbox(format("There's nothing but empty papers in the bag."))
		releaseall
		end
	}
	msgbox(format("The professor has left a note on this bag...\p"
		"{COLOR GREEN}{PLAYER}, seeing as you won't have brought any Pokémon to Tosa with you I've left you with this Fennekin. I know that you'll take great care of it!"), MSGBOX_DEFAULT)
	msgbox(format("You open the bag and find a Poké Ball.\p"
		"Do you want to take it?"), MSGBOX_YESNO)
	if (var(VAR_RESULT) == 1) {
		playse(SE_CONTEST_ICON_CLEAR)
		waitse
		msgbox(format("You take the Poké Ball."))
		givemon(SPECIES_FENNEKIN, 5, ITEM_ORAN_BERRY)
		playfanfare(MUS_OBTAIN_ITEM)
		msgbox(format("You received a Fennekin!"))
		waitfanfare
		setflag(FLAG_SYS_POKEMON_GET)
		setflag(FLAG_FOUNDSTARTER)
		// Nicknaming!
		msgbox(format("Should I give Fennekin a nickname?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == 1) {
			// Nickname Fennekin
			setvar(VAR_0x8004, 0) // Reset nickname var.
			fadescreen(FADE_TO_BLACK)
			special(ChangePokemonNickname)
			waitstate
			bufferpartymonnick(2, 0) // Buffer Fennekin's nickname.
			msgbox(format("{STR_VAR_2}...a perfect name.\p""You close the bag.")) // For some reason STR VAR 1 didn't work here.
			releaseall
			end
		} else {
			msgbox(format("You decide not to give Fennekin a nickname and close the bag."))
			releaseall
			end
		}
		
	} else {
		msgbox(format("You decide not to take the Poké Ball."))
		releaseall
		end
	}

}

script LittlerootTown_EventScript_Gatekeeper_CheckPermit {
	lockall
	faceplayer

	// Use a switch statement here with a variable that tracks the state of the gatekeeper.

	if(flag(FLAG_RESERVE_GATE_OPEN)) { // If the guard has moved out of the way, just show the message.
		faceplayer
		msgbox(format("The Tosa Nature Reserve is a\n"
			"beautiful place, isn't it?\p"
			"Please take care of it while you're here!"))
		releaseall
		end

	} elif(checkitem(ITEM_TRAINER_PERMIT) == 0 || !flag(FLAG_SYS_POKEMON_GET)) { // If this player doesn't have a Trainer Permit, show the message.
		msgbox(format("Hey!\p"
			"You need a {COLOR GREEN}Trainer Permit{COLOR DARK_GRAY} to enter the\n"
			"nature reserve."), MSGBOX_DEFAULT)
		releaseall
		end

	} else { // The trainer has the permit, and the guard moves out of the way.
		setflag(FLAG_RESERVE_GATE_OPEN) // Hides the gatekeepers initial position.
		msgbox(format("You flash your Trainer Permit..."), MSGBOX_AUTOCLOSE)
		playse(SE_PIN)
		applymovement(4, moves(
			emote_exclamation_mark
		))
		delay(20)
		waitmovement(0)
		msgbox(format("Ah! That's a {COLOR GREEN}Trainer Permit{COLOR DARK_GRAY}!\n"
			"Go on through."), MSGBOX_AUTOCLOSE)
		applymovement(OBJ_EVENT_ID_PLAYER, moves(
			walk_right
			face_left
		))
		waitmovement(0)
		applymovement(4, moves(
			walk_down
			walk_left
			face_down
		))
		waitmovement(4)
		setvar(VAR_PORT_TOSA_STATE, 1) // Iterates the state variable, this stops FLAG_HIDE_RESERVE_GATEKEEPER from being set again.
		clearflag(FLAG_HIDE_RESERVE_GATEKEEPER) // Clears the flag that hides the second position of the gatekeeper.
		// removeobject(4)
		releaseall
		end
	}
}


script LittlerootTown_EventScript_Gatekeeper_GateOpen {
	lockall
	faceplayer
	msgbox(format("The Tosa Nature Reserve is a\n"
		"beautiful place, isn't it?\p"
		"Please take care of it while you're here!"))
	releaseall
	end	
}